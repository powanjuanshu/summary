@echo off
set a=%1
echo %a%
----------------------------------
@echo off
set num1=12
set num2=21
::会执行数学计算,sum计算后为33
set /a sum=num1+num2
set name=王大雷
echo %name%
set /p password=请输入密码:
echo 您输入的密码为%password%
::会显示所有的系统环境变量(包括上面的num1=12,num2=21,sum=33)
::set
goto comment
 
:comment
----------------------------------
set用于设置一个变量.当变量需要数学运算时用set /a
set /p设置一个从外部读取的变量.如果仅仅输入set会显示出所有的系统环境变量.
/p = prompt,就是打印提示信息,等待用户输入.例子:set /p vv="请输入一个数:"
----------------------------------
set /a 执行数学计算
set /p 提示用户输入
----------------------------------
批处理SET命令的详细介绍
一、用set命令设置自定义变量
显示、设置或删除cmd.exe环境变量.
SET [variable[=string]]

variable 指定环境变量名.
string 指定要指派给变量的一系列字符串.

要显示当前环境变量,键入不带参数的SET.
SET命令不允许变量名含有等号
例子:
@echo off
set var=我是值
echo %var%
pause

请看set var=我是值,这就是BAT直接在批处理中设置变量的方法
set是命令,var是变量名,=号右边的"我是值"是变量的值
在批处理中我们要引用这个变就把var变量名用两个%(百分号)扩起来,如%var%

SET还可以提供一个交互界面,让用户自己输入变量的值,然后我们在来根据这个值来做相应操作,现在我就来说说SET的这种语法,只需要加一个"/P"参数就可以
SET /P variable=[promptString]
例子: 
@echo off
set /p var=请输入变量的值:
echo 您输入了%var%
pause

set /p 是命令语法
var是变量名,=号右边的"请输入变量的值: ",这个是提示语,不是变量的值了
运行后,我们在提示语后面直接输入1,就会显示一行您输入了1

先回顾到这,现在讲SET其他功能
使用set /?查看SET的帮助我们发现SET除了我上面讲的
SET [variable=[string]]
SET /P variable=[promptString]

这两种语法外,还有如下几种语法:
SET /A expression
环境变量替换已如下增强:
%PATH:str1=str2%
%PATH:~10,5%
%PATH:~-10%
%PATH:~0,-2%
这机种语法有什么用处呢?下面我们来一个个讲解他们

二、用set命令进行简单计算
语法:SET /A expression
/A 命令行开关指定等号右边的字符串为被评估的数字表达式.该表达式评估器以递减的优先权顺序支持下列操作:
()
-分组
! ~ -
-一元运算符
* / %
-算数运算符
+ -
-算数运算符
<< >>
-二进制逻辑移位
&
-二进制按位“与”
^
-二进制按位“异”
|
-二进制按位“或”
= *= /= %= += -= -算数赋值
&= ^= |= <<= >>= -二进制运算赋值
,
-表达式分隔符

上面这些是系统帮助里的内容,看着是不是有点晕,没关系我来简单解释一下:
set的/A参数就是让SET可以支持数学符号进行加减等一些数学运算!
现在开始举例子介绍这些数学符号的用法:
例: 
@echo off
set /p input=请输入计算表达式:
set /a var=%input%
echo 计算结果:%input%=%var%
pause

上面的例子是龙卷风设计的,很好用哟,请看下面几个运算过程:
注意:DOS计算只能精确到整数
请输入计算表达式:1+9+20+30-10
计算结果:1+9+20+30-10=50
请按任意键继续. . .
请输入计算表达式:10/3
计算结果:10/3=3
#DOS计算精确到整数,小数舍了.
请按任意键继续. . .
请输入计算表达式:-100+62
计算结果:-100+62=-38
请按任意键继续. . .
请输入计算表达式:100%3 ＃求余数
计算结果:100%3=1
请按任意键继续. . .
请输入计算表达式:(25+75)*2/(15+5)
计算结果:(25+75)*2/(15+5)=10
请按任意键继续. . .
请输入计算表达式:1234567890*9876543210
无效数字.数字精确度限为 32 位.
计算结果:1234567890*9876543210=
请按任意键继续. . .
注意:上面的计算过程显示,DOS计算只能精确到32位,这个32位是指二进制32位,其中最高位为符号位（0为正,1为负）,低位31位为数值.31个1换成十进制为2147483647,所以DOS计算的有效值范围是-2147483647至2147483647,超出该数值范围时计算出错,请看下面的计算过程:
请输入计算表达式:2147483647-1 ＃最大值减1,值有效
计算结果:2147483647-1=2147483646
请按任意键继续. . .
运行set /a a=1+1,b=2+1,c=3+1后会显示一个4,
但我们用echo %a% %b% %c%后看结果,会发现其他数学运算也有效果,这就是"逗"号的作用

有时候我们需要直接在原变量进行加减操作就可以用这种语法
set /a var+=1

这样的语法对应原始语法就是set /a var = %var% + 1
都是一样的结果,在原变量的值上在进行数学运算,不过这样写简单一点

再来一个:
set /a var*=2
其他都这么用,只要帮助里有这个语法!
另外还有一些用逻辑或取余操作符,这些符号,按照上面的使用方法会报错的
比如我们在CMD里输入set /a var=1 & 1 "与运算",他并不会显示为1,而是报错,
为什么?对于这样的"逻辑或取余操作符",我们需要把他们用双引号引起来,也可以用转义字符^,看例子
set /a var= 1 "&" 1 这样结果就显示出来了,其他逻辑或取余操作符用法
set /a var= 1 "+" 1 异运算
set /a var= 1 "%" 1

取模运算
set /a var= 3 "<<" 2 左移位运算, 3的二进制为11,左移2位为1100,换成十进制就是12,自行验证
set /a var= 4 ">>" 2右移位运算,4的二进制为100,右移动2位为1,结果为1
还有几个数学不太行,搞不清楚了....不列出来了,
龙卷风补充:凡是按位计算均需换算成二进制,下面行中的符号均针对二进制
这些符号也可以用&= ^= |= <<= >>= 这样的简单用法如
set /a var"&=" 1 等于set /a var = %var% "&" 1 注意引号
思考题:求2的n次方
答案: 
@echo off
set /p n=请输入2的几次方: 
set /a num=1^<^<N
echo %num%
pause

三、用set命令进行字符串处理
1、字符串替换
好了,符号说到这,现在说%PATH:str1=str2%
上面语法的意思就是:将字符串变量%PATH%中的str1替换为str2
这个是替换变量值的内容,看例子
@echo off
set a= bbs. verybat. cn
echo 替换前的值: "%a%"
set var=%a: =%
echo 替换后的值: "%var%"
pause

运行显示:
替换前的值:" bbs. verybat. cn"
替换后的值:"bbs.verybat.cn"

对比一下,我们发现他把变量%a%的空格给替换掉了,从这个例子,我们就可以发现
%PATH:str1=str2%这个操作就是把变量%PATH%的里的str1全部用str2替换
比如我们把上面的例子改成这样 
@echo off
set a=bbs.verybat.cn
echo 替换前的值: "%a%"
set var=%a:.=伤脑筋%
echo 替换后的值: "%var%"
pause

运行显示:
替换前的值: "bbs.verybat.cn"
替换后的值: "bbs伤脑筋verybat伤脑筋cn"

解释set var=%a:.=伤脑筋%
set是命令 var是变量名 字a是要进行字符替换的变量的值,"."为要替换的值,
"伤脑筋"为替换后的值!
执行后就会把变量%a%里面的"."全部替换为"伤脑筋"
这就是set的替换字符的很好的功能! 替换功能先讲到这,下面将字符串截取功能

2、字符串截取
**********************************************
截取功能统一语法格式为:%a:~[m[,n]]%
**********************************************
方括号表示可选,%为变量标识符,a为变量名,不可少,冒号用于分隔变量名和说明部分,符号～可以简单理解为“偏移”即可,m为偏移量（缺省为0）,n为截取长度（缺省为全部）
%PATH:~10,5%

这个什么意思,看例子:
截取功能例子1: 
复制内容到剪贴板代码:@echo off
set a=bbs.verybat.cn
set var=%a:~1,2%
echo %var%
pause
执行后,我们会发现只显示了"bs"两个字母,我们的变量%a%的值不是为bbs.verybat.cn吗？
怎么只显示了第2个字母和第3个字母"bs",分析一结果我们就可以很容易看出
%PATH:~10,5%就是显示变量PATH里从11位（偏移量10）开始的5个字符!
分析set var=%a:~1,2%

set是命令,var是变量值,a要进行字符操作的变量,"1"从变量"a"第几位开始显示,"2"表示显示几位.
合起来就是把变量a的值从第2位（偏移量1）开始,把2个字符赋予给变量var
这样应该明白了吧~
其他两种语法
%PATH:~-10%
%PATH:~0,-2%
他们也是显示指定变量指定几位的值的意思
%PATH:~-10% 看例子
截取功能例子2: 
复制内容到剪贴板代码:@echo off
set a=bbs.verybat.cn
set var=%a:~-3%
echo %var%
pause
运行结果:.cn
这个就是把变量a倒数3位的值给变量VAR
当然我们也可以改成这样
截取功能例子3: 
复制内容到剪贴板代码:@echo off
set a=bbs.verybat.cn
set var=%a:~3%
echo %var%
pause
运行显示:.verybat.cn
这个就是把变量a的从第3位开始后面全部的值给变量VAR
%PATH:~0,-2%

例子
截取功能例子4:

[/code]@echo off
set a=bbs.verybat.cn
set var=%a:~0,-3%
echo %var%
pause
执行后,我们发现显示的是"bbs.verybat",少了".cn"
从结果分析,很容易分析出,这是把变量a的值从0位开始,
到倒数第三位之间的值全部赋予给var
如果改成这样
截取功能例子5:
复制内容到剪贴板代码:@echo off
set a=bbs.verybat.cn
set var=%a:~2,-3%
echo %var%
pause
运行显示:s.verybat
那么他就是显示从第3位（偏移量2）开始减去倒数三位字符的值,并赋予给变量var
讲得好,例子就是说明问题,为便于记忆,龙卷风小节如下:
a=bbs.verybat.cn
%a:~1,2% ＝“bs” 偏移量1,从第二位开始向右取2位
%a:~-3% ＝“.cn”

偏移量负3,即倒数3位（也可理解为留下右边3位）,右取全部
%a:~3%

＝“.verybat.cn” 偏移量3（也可理解为去掉左边3位）,右取全部
%a:~0,-3% ＝“bbs.verybat” 偏移量0,右取长度至负3,即倒数3位
%a:~2,-3% ＝“s.verybat”

偏移量2,右取长度至负3,即倒数3位
**********************************************
所以,截取功能统一语法格式为:%a:~[m[,n]]%
**********************************************
方括号表示可选,%a%为变量名,不可少,冒号用于分隔变量名和说明部分,符号～可以简单理解为“偏移”即可,m为偏移量（缺省为0）,n为截取长度（缺省为全部）
上面所述用法其实相当于vbs函数mid、left、right
%a:~0,n%

相当于函数left(a,n)

取左边n位
%a:~-m%

相当于函数right（a,m） 取右边m位
%a:~m,n% 相当于函数mid(a,m+1,n) 从m+1位开始取n位
%a:~m,-n% 相当于函数mid(a,m+1,len(a)-m-n)
%a:~m %

相当于函数mid(a,m+1,len(a)-m) 或者right(a,len(a)-m)
思考题目:输入任意字符串,求字符串的长度
参考答案:
@echo off
set /p str=请输入任意长度的字符串:
echo 你输入了字符串:"%str%"
if not defined str (pause & goto :eof)
set num=0
:len
set /a num+=1
set str=%str:~0,-1%
if defined str goto len
echo 字符串长度为:%num%
pause











